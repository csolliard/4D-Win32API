3-Feb-2003

To enable a toolbar, put the following code into a method that is run inside its own local process. You can start this local process in the On Startup database method.

The only things you need to modify are the height in pixels of your toolbar form ($lToolBarHeight), and the actual form to open in the Dialog command at the very end. You can also change $bHideTitleBarWhenMaximized to False if you'd like to still see the title bar below the toolbar when a window is maximized.

 

  ` toolbar_main

  ` Jeffrey Kain, 1/16/2003

  ` This is a sample to show how to use the Win32API gui_RestrictToolbar
  ` command.

C_LONGINT($x;$lTop;$lErr;$lCaptionHeight;$lToolBarHeight)
C_BOOLEAN($bHideTitleBarWhenMaximized)

MENU BAR(1)
MESSAGES OFF

  ` When a resize of the MDI window occurs, the plugin will send
  ` an outside call form event to the processes that have called
  ` gui_RespectToolbar.

  ` Since multiple processes may be toolbars, an array is returned
  ` with elements set to non-zero for each process to check and
  ` then clear:

  ` Element 1: Left toolbar
  ` Element 2: Top toolbar
  ` Element 3: Right toolbar
  ` Element 4: Bottom toolbar

  ` This is a "top" toolbar, so we'll be sure to initialize
  ` the second element of <>TB_NOTIFICATION to zero
  ` before we begin.
ARRAY LONGINT(<>TB_NOTIFICATION;4)
<>TB_NOTIFICATION{2}:=0

  ` This variable can be used to control whether you want to see the window
  ` title bar when maximized, immediately below the toolbar, or if you want
  ` the window title bar hidden.
$bHideTitleBarWhenMaximized:=True

$lToolBarHeight:=33  ` height in pixels of the toolbar dialog
$lCaptionHeight:=sys_GetWindowMetrics (WM_CAPTION_HEIGHT )  ` height in pixels of a normal window title bar

$lErr:=gui_SubClassInit (RW_SUBCLASS_INIT )  ` Enable intercepting of Windows messages  
  ` Set the number of pixels to adjust 4D's vertical resizing
If ($bHideTitleBarWhenMaximized)
  $lErr:=gui_RespectToolBar ($lToolBarHeight-$lCaptionHeight;"T";$lToolBarHeight)
Else 
  $lErr:=gui_RespectToolBar ($lToolBarHeight+(sys_GetWindowMetrics (WM_BORDER_HEIGHT )*2);"T")  ` palette windows are one pixel smaller than a standard caption
End if 


  ` Find the top coordinate of the toolbar using Win32 calls to take into 
  ` account the OS and visual theme currently in use
$lTop:=sys_GetWindowMetrics (WM_BORDER_HEIGHT )*3+sys_GetWindowMetrics (WM_MENU_HEIGHT )-3

  ` Open the toolbar
$x:=Open window(0;$lTop;1800;$lTop+$lToolBarHeight;-1*Palette window )
DIALOG([dialogs];"Toolbar")
CLOSE WINDOW
